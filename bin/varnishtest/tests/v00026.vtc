varnishtest "Hash director"

server s1 {
	rxreq
	txresp -hdr "Foo: 1" -body "1"
	rxreq
	txresp -hdr "Foo: 3" -body "3"
} -start

server s2 {
	rxreq
	txresp -hdr "Foo: 2" -body "2"
	rxreq
	txresp -hdr "Foo: 4" -body "4"
	rxreq
	txresp -hdr "Foo: 6" -body "6"
	rxreq
	txresp -hdr "Foo: 8" -body "8"
} -start


varnish v1 -vcl+backend {
	import ${vmod_directors};

	sub vcl_init {
		new h1 = directors.hash();
		h1.add_backend(s1, 1);
		h1.add_backend(s2, 1);
	}

	sub vcl_recv {
		return(pass);
	}
	sub vcl_backend_fetch {
		if (bereq.url == "/nohdr") {
			set bereq.backend = h1.backend(bereq.http.Void);
		} else if (bereq.url == "/emptystring") {
			set bereq.backend = h1.backend("");
		} else if (bereq.url == "/13") {
			set bereq.backend = h1.backend(bereq.http.Void + "" + bereq.url);
		} else {
			set bereq.backend = h1.backend(bereq.url);
		}
	}
} -start

client c1 {
	txreq -url /1
	rxresp
	expect resp.http.foo == "1"

	txreq -url /3
	rxresp
	expect resp.http.foo == "2"

	txreq -url /13
	rxresp
	expect resp.http.foo == "3"

	txreq -url /8
	rxresp
	expect resp.http.foo == "4"

	txreq -url /emptystring
	rxresp
	expect resp.http.foo == "6"

	txreq -url /nohdr
	rxresp
	expect resp.http.foo == "8"
} -run
