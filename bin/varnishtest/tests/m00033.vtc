varnishtest "VMOD blob IDENTITY encode and decode"

varnish v1 -vcl {
	import blob;
	backend b { .host = "${bad_ip}"; }

	sub vcl_recv {
	  return(synth(200));
	}

	sub vcl_synth {
	    set resp.http.id =
	      blob.encode(IDENTITY, blob.decode(IDENTITY,
			      "The quick brown fox jumps over the lazy dog"));

	    set resp.http.hobbes =
	      blob.encode(IDENTITY, blob.decode(IDENTITY,
{"Man is distinguished, not only by his reason, but by this singular passion from other animals, which is a lust of the mind, that by a perseverance of delight in the continued and indefatigable generation of knowledge, exceeds the short vehemence of any carnal pleasure."}));

	    set resp.http.list =
	      blob.encode(IDENTITY, blob.decode(IDENTITY,
			      "" + req.http.unset + req.url +
			      "The quick brown fox jumps over " +
			      req.http.unset + "" + req.http.unset + "" +
			      "the lazy dog" + req.url + req.http.unset + ""));

	    set resp.http.empty =
	      blob.encode(IDENTITY, blob.decode(IDENTITY, ""));

	    set resp.http.undef =
	      blob.encode(IDENTITY, blob.decode(IDENTITY,
						      req.http.unset));

	    set resp.http.emptylist =
	      blob.encode(IDENTITY, blob.decode(IDENTITY,
			      req.http.unset + "" + req.http.unset + ""));

	    set resp.http.param =
	      blob.encode(blob=
			      blob.decode(encoded=
				 "The quick brown fox jumps over the lazy dog",
					    decoding=IDENTITY),
			      encoding=IDENTITY);

	    set resp.http.paramlist =
	      blob.encode(IDENTITY, blob.decode(encoded=
			      "" + req.http.unset + req.url +
			      "The quick brown fox jumps over " +
			      req.http.unset + "" + req.http.unset + "" +
			      "the lazy dog" + req.url + req.http.unset + "",
			      decoding=IDENTITY));

	    set resp.http.truncated =
	      blob.encode(IDENTITY, blob.decode(HEX, "666f6f00626172"));
	}
} -start

client c1 {
	txreq -url "/"
	rxresp
	expect resp.http.id == "The quick brown fox jumps over the lazy dog"
	expect resp.http.hobbes == "Man is distinguished, not only by his reason, but by this singular passion from other animals, which is a lust of the mind, that by a perseverance of delight in the continued and indefatigable generation of knowledge, exceeds the short vehemence of any carnal pleasure."
	expect resp.http.list == "/The quick brown fox jumps over the lazy dog/"
	expect resp.http.empty == ""
	expect resp.http.undef == ""
	expect resp.http.emptylist == ""
	expect resp.http.param == "The quick brown fox jumps over the lazy dog"
	expect resp.http.paramlist == "/The quick brown fox jumps over the lazy dog/"
	expect resp.http.truncated == "foo"
} -run
