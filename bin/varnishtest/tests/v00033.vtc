varnishtest "Stevedore variables and BYTES type test"


server s1 {
	rxreq
	txresp -bodylen 4
	rxreq
	txresp -bodylen 5
} -start

varnish v1 -vcl+backend {

	sub vcl_backend_response {
		set beresp.http.foo =
		    storage.nowhere.free_space +
		    1 B + 1 KB + 1 MB + 1GB + 1TB;
		if (bereq.url == "/foo") {
			set beresp.storage_hint = "Transient";
		}
	}
	sub vcl_deliver {
		set resp.http.bar = storage.Transient.used_space > 0B;
	}
} -start

client c1 {
	txreq
	rxresp
	expect resp.status == 200
	expect resp.http.foo == 1100586419201.000
	expect resp.http.bar == false

	txreq -url /foo
	rxresp
	expect resp.status == 200
	expect resp.http.foo == 1100586419201.000
	expect resp.http.bar == true
} -run

varnish v1 -errvcl {Expected BYTES unit (B, KB, MB...) got '"X"'} {
	sub vcl_recv {
		if (storage.nowhere.free_space > 4 "X") {
		}
	}
}
varnish v1 -errvcl {Unknown BYTES unit 'X'.  Legal are 'B', 'KB', 'MB', 'GB' and 'TB'} {
	sub vcl_recv {
		if (storage.nowhere.free_space > 4 X) {
		}
	}
}
