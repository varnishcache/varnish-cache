varnishtest "chunked req.body"

server s1 {
	rxreq
	expect req.bodylen == 106
	txresp -body "ABCD"
	rxreq
	expect req.bodylen == 108
	txresp -body "ABCDE"
} -start

varnish v1 -vcl+backend {
} -start

varnish v1 -cliok "param.set debug +syncvsl"

client c1 {
	txreq -req POST -nolen -hdr "Transfer-encoding: chunked"
	chunked {BLA}
	delay .2
	chunkedlen 100
	delay .2
	chunked {FOO}
	delay .2
	chunkedlen 0
	rxresp
	expect resp.status == 200
	expect resp.bodylen == 4
} -run

delay .2

varnish v1 -cliok "param.set vcc_allow_inline_c true" -vcl+backend {
	import ${vmod_std};

	sub vcl_recv {
		std.cache_req_body(1000B);
	}
}

client c1 {
	txreq -req POST -nolen -hdr "Transfer-encoding: chunked"
	chunked {BLAS}
	delay .2
	chunkedlen 100
	delay .2
	chunked {TFOO}
	delay .2
	chunkedlen 0
	rxresp
	expect resp.status == 200
	expect resp.bodylen == 5
} -run
