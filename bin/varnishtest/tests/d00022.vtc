varnishtest "shard director Restarts"

server s1 {
	rxreq
	txresp -body "ech3Ooj"
	rxreq
	txresp -body "ech3Ooj"
	rxreq
	txresp -body "ech3Ooj"
} -start

server s2 {
	rxreq
	txresp -body "ieQu2qua"
	rxreq
	txresp -body "ieQu2qua"
	rxreq
	txresp -body "ieQu2qua"
} -start

server s3 {
	rxreq
	txresp -body "xiuFi3Pe"
	rxreq
	txresp -body "xiuFi3Pe"
	rxreq
	txresp -body "xiuFi3Pe"
} -start

varnish v1 -vcl+backend {
	import directors;

	sub vcl_init {
		new vd = directors.shard();
		vd.add_backend(s1);
		vd.add_backend(s2);
		vd.add_backend(s3);
		vd.reconfigure(25);
	}

	sub vcl_recv {
		set req.backend_hint = vd.backend(by=KEY,
		    key=1756955383,
		    alt=req.restarts,
		    healthy=ALL);

		if (req.url == "/2" && req.restarts > 0) {
			unset req.http.vrstart;
		}
		if (req.url == "/3" && req.restarts > 1) {
			unset req.http.vrstart;
		}
		return(pass);
	}

	sub vcl_deliver {
		if (req.http.vrstart) {
			return(restart);
		}
	}

} -start


client c1 {
	txreq -url /1
	rxresp
	expect resp.body == "ech3Ooj"

	txreq -url /2 -hdr "vrstart: 1"
	rxresp
	expect resp.body == "ieQu2qua"

	txreq -url /3 -hdr "vrstart: 1"
	rxresp
	expect resp.body == "xiuFi3Pe"
} -run
