varnishtest "test ban + vary behavior"

server s0 {
	rxreq
	txresp -hdr "vary: version" -body "Variant A"
	rxreq
	txresp -hdr "vary: version" -body "Variant B"
	rxreq
	txresp -hdr "vary: version" -body "New variant A"
	rxreq
	txresp -hdr "vary: version" -body "New variant B"
} -start

varnish v1 -vcl+backend {} -start

client c1 {
	txreq -hdr "version: a"
	rxresp
	expect resp.body == "Variant A"
} -run

client c2 {
	txreq -hdr "version: b"
	rxresp
	expect resp.body == "Variant B"
} -run

varnish v1 -cliok "ban req.http.version == a"

# Should this remove a single variant from cache
client c3 {
	txreq -hdr "version: a"
	rxresp
	expect resp.body == "New variant A"
} -run

client c4 {
	txreq -hdr "version: b"
	rxresp
	expect resp.body == "Variant B"
} -run
