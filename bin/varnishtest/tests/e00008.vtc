varnishtest "ESI parsing errors"

server s1 {
	rxreq
	txresp -body {
		<html>					1
		Before include				2
		<!--esi <esi:include src="/body"/> -->	3
		After include				4
		<!-- normal comment -->			5
		<esi:bogosity/>				6
		<esi:comment>				7
		 foo					8
		 </esi:comment>				9
		<esi:comment bar />			10
							11
		<esi:remove>				12
			<esi:comment foo/>		13
		</esi:remove>				14
							15
		<esi:remove>				16
			<esi:remove>			17
			bar				18
			</esi:remove>			19
		</esi:remove>				20
		</>					21
		<esi:remove/>				22
							23
		<esi:include				24
	
                                                             >	25
							26
		<esi:include  aaa />			27
		<esi:include  k?=./>			28
		<esi:include  src/>			29
		<esi:include  src=/>			30
		<esi:include  src= />			31
		</esi:include>				32
		<esi:include  src="foofof />		33
		<esi:include  foo=bar src=/body2 />	34
	}
	rxreq
	expect req.url == "/body"
	txresp -body {
		Included file
		<esi:remove>
		<Aaa
	}
	rxreq
	expect req.url == "/body2"
	txresp -body {
		Included file 2
		<!--esi
		<![CDATA[fooo
	}
} -start

varnish v1 -vcl+backend {
	sub vcl_backend_response {
		set beresp.do_esi = true;
	}
} -start

varnish v1 -cliok "param.set debug +esi_chop"

varnish v1 -cliok "param.set debug +syncvsl"

client c1 {
	txreq
	rxresp
	expect resp.status == 200
	expect resp.bodylen == 385
}

client c1 -run
varnish v1 -expect esi_errors == 15
