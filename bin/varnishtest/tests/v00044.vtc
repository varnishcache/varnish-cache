varnishtest "vcl.state coverage tests"

server s1 -repeat 20 {
	rxreq
	txresp
	delay .2
	close
} -start

# The debug vmod logs temperature vcl events
varnish v1 -arg "-p vcl_cooldown=1" -vcl {
	import ${vmod_debug};
	backend default {
		.host = "${s1_addr}";
		.probe = { .interval = 1s; .initial = 1;}
	}
} -start

# We only have one vcl yet
varnish v1 -expect VBE.vcl1.default.happy >= 0
varnish v1 -expect !VBE.vcl2.default.happy
varnish v1 -cliok "backend.list -p *.*"

varnish v1 -vcl {
	backend default {
		.host = "${s1_addr}";
		.probe = { .interval = 1s; .initial = 1;}
	}
}

# Now we have two vcls (and run on the latest loaded)
delay .4
varnish v1 -expect VBE.vcl1.default.happy >= 0
varnish v1 -expect VBE.vcl2.default.happy >= 0

# Freeze the first VCL
varnish v1 -cliok "vcl.state vcl1 cold"
delay .4
varnish v1 -expect !VBE.vcl1.default.happy

# Set it auto should be a no-op
varnish v1 -cliok "vcl.state vcl1 auto"
delay .4
varnish v1 -expect !VBE.vcl1.default.happy

# Use it, and it should come alive
varnish v1 -cliok "vcl.use vcl1"
delay .4
varnish v1 -expect VBE.vcl1.default.happy >= 0
varnish v1 -expect VBE.vcl2.default.happy >= 0

# and the unused one should go cold
delay 4
varnish v1 -expect !VBE.vcl2.default.happy

# Mark the used warm and use the other
varnish v1 -cliok "vcl.state vcl1 warm"
varnish v1 -cliok "vcl.use vcl2"

# It will stay warm even after the cooldown period
delay 4
varnish v1 -expect VBE.vcl1.default.happy >= 0
varnish v1 -expect VBE.vcl2.default.happy >= 0

# You can't freeze the active VCL
varnish v1 -clierr 106 "vcl.state vcl2 cold"

# However a warm event is guaranteed...
logexpect l1 -v v1 -g raw {
	expect * 0 Debug "vcl1: VCL_EVENT_COLD"
	expect * 0 Debug "vcl1: VCL_EVENT_WARM"
} -start

# ...when you use a cold VCL
varnish v1 -cliok "vcl.state vcl1 cold"
varnish v1 -cliok "vcl.use vcl1"

logexpect l1 -wait

# It will apply the cooldown period once inactive
varnish v1 -cliok "vcl.use vcl2"
delay .4
varnish v1 -expect VBE.vcl1.default.happy >= 0
delay 4
varnish v1 -expect !VBE.vcl1.default.happy

# A VMOD's warm-up can fail
varnish v1 -cliok "param.set max_esi_depth 42"
varnish v1 -clierr 300 "vcl.state vcl1 warm"

# A warm-up failure can also fail a child start
varnish v1 -cliok stop
varnish v1 -cliok "vcl.state vcl1 warm"
varnish v1 -clierr 300 start
