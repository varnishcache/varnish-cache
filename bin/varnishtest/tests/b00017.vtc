varnishtest "Check set resp.body & storage in vcl_synth"

varnish v1 -arg "-sTransient=debug,lessspace -ssynth=synth" -vcl {
	backend foo {
		.host = "${bad_backend}";
	}
	sub vcl_recv {
		return (synth(888));
	}

	sub vcl_synth {
		if (! req.http.synth) {
			set resp.storage = storage.Transient;
		}
		set resp.body = "Custom vcl_synth's " + now + " body";
		set resp.http.storage = resp.storage;
		return (deliver);
	}
} -start

client c1 {
	txreq -url "/"
	rxresp
	expect resp.status == 888
	expect resp.http.connection != close
	expect resp.bodylen >= 52
	expect resp.bodylen <= 53
	expect resp.body ~ "^Custom vcl_synth's .* body$"
	expect resp.http.storage == "storage.Transient"

	txreq -url "/" -hdr "synth: true"
	rxresp
	expect resp.status == 888
	expect resp.http.connection != close
	expect resp.bodylen >= 52
	expect resp.bodylen <= 53
	expect resp.body ~ "^Custom vcl_synth's .* body$"
	expect resp.http.storage == "storage.synth"
} -run

varnish v1 -expect s_synth == 2
