varnishtest "Check set resp.body & storage in vcl_synth"

varnish v1 -arg "-sTransient=debug,lessspace -smalloc=malloc" -vcl {
	backend foo {
		.host = "${bad_backend}";
	}
	sub vcl_recv {
		return (synth(888));
	}

	sub vcl_synth {
		if (req.http.malloc) {
			set resp.storage = storage.malloc;
		}
		set resp.body = "Custom vcl_synth's body";
		set resp.http.storage = resp.storage;
		return (deliver);
	}
} -start

client c1 {
	txreq -url "/"
	rxresp
	expect resp.status == 888
	expect resp.http.connection != close
	expect resp.bodylen == 23
	expect resp.body == "Custom vcl_synth's body"
	expect resp.http.storage == "storage.Transient"

	txreq -url "/" -hdr "malloc: true"
	rxresp
	expect resp.status == 888
	expect resp.http.connection != close
	expect resp.bodylen == 23
	expect resp.body == "Custom vcl_synth's body"
	expect resp.http.storage == "storage.malloc"
} -run

varnish v1 -expect s_synth == 2
