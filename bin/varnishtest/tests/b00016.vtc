varnishtest "Check naming of backends"

varnish v1 -vcl { 
	backend foo {
		.host = "${bad_ip}"; .port = "9080";
	}

	sub vcl_recv {
	    error 200 "ok";
	}

	sub vcl_error {
	    set obj.http.X-Backend-Name = req.backend;
	}
} -start

client c1 {
	txreq -url "/"
	rxresp
	expect resp.http.X-Backend-Name == "foo"
} -run

varnish v1 -vcl { 
	director bar random {
		{
		.backend = {
		 	.host = "${bad_ip}"; .port = "9080";
		}
		.weight = 1;
		}
	}

	sub vcl_recv {
	    error 200 "ok";
	}

	sub vcl_error {
	    set obj.http.X-Backend-Name = req.backend;
	}
}

client c1 {
	txreq -url "/"
	rxresp
	expect resp.http.X-Backend-Name == "bar"
} -run

varnish v1 -vcl { 
	import directors from "${topbuild}/lib/libvmod_directors/.libs/libvmod_directors.so" ;
	backend b1 {
		.host = "${bad_ip}"; .port = "9080";
	}
	sub vcl_init {
		new baz = directors.round_robin();
		baz.add_backend(b1);
	}

	sub vcl_recv {
	    set req.backend = baz.backend();
	    error 200 "ok";
	}

	sub vcl_error {
	    set obj.http.X-Backend-Name = req.backend;
	}
}

client c1 {
	txreq -url "/"
	rxresp
	expect resp.http.X-Backend-Name == "baz"
} -run
