varnishtest "purging on POST"

server s1 {
	rxreq
	expect req.request == "GET"
	txresp -body "1"

	rxreq
	expect req.request == "POST"
	txresp -body "22"

	rxreq
	expect req.request == "POST"
	txresp -body "333"

	rxreq
	expect req.request == "GET"
	txresp -body "4444"
} -start

varnish v1 -vcl+backend {
	sub vcl_recv {
		if (req.request == "POST") {
			/* Lookup so we find any cached object */
			return (lookup);
		}
	}
	sub vcl_hit {
		if (req.request == "POST") {
			/* Get rid of this object */
			set obj.ttl = 0s;
			return (pass);
		}
	}
	sub vcl_miss {
		if (req.request == "POST") {
			/* Make sure we don't cache the POST result */
			return (pass);
		}
	}
} -start

client c1 {
	txreq
	rxresp
	expect resp.bodylen == 1

	txreq -req POST
	rxresp
	expect resp.bodylen == 2

	txreq -req POST
	rxresp
	expect resp.bodylen == 3

	txreq
	rxresp
	expect resp.bodylen == 4
} -run
