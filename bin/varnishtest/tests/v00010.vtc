varnishtest "VCL: check panic and restart"

server s1 {
	rxreq
	txresp -hdr "Foo: bar" -body "abcdef\n"
	rxreq
	txresp -hdr "Panic: fetch" -body "012345\n"
	close

	sema r1 sync 2
	accept
	rxreq
	txresp -hdr "Foo: bar" -body "abcdef\n"
	rxreq
	txresp -hdr "Panic: deliver" -body "012345\n"
	close

	sema r1 sync 2
	accept
	rxreq
	txresp -hdr "Foo: foo" -body "abcdef\n"
} -start

varnish v1 -arg "-smalloc,1m" -vcl+backend {

	import ${vmod_debug};

	sub vcl_backend_response {
		if (beresp.http.panic == "fetch") {
			debug.panic("Had Panic header: " + beresp.http.panic);
		}
	}
	sub vcl_deliver {
		if (resp.http.panic == "deliver") {
			debug.panic("Had Panic header: " + resp.http.panic);
		}
	}
} -start

varnish v1 -cliok "param.set feature +no_coredump"

# Force the (random) port selected to be used again after restart.
varnish v1 -cliok "param.set listen_address ${v1_addr}:${v1_port}"

client c1 {
	txreq -url "/"
	rxresp
	txreq -url "/foo"
	# Don't expect answer, the server crashed.
} -run

varnish v1 -wait-stopped
varnish v1 -cliok "panic.show"
varnish v1 -cliok "panic.clear"
varnish v1 -clierr 300 "panic.clear"
varnish v1 -cliok "start"
varnish v1 -wait-running
sema r1 sync 2

delay 0.5

client c1 {
	txreq -url "/"
	rxresp
	txreq -url "/foo"
	# Don't expect answer, the server crashed.
} -run

varnish v1 -wait-stopped
varnish v1 -cliok "panic.show"
varnish v1 -cliok "panic.clear"
varnish v1 -clierr 300 "panic.clear"
varnish v1 -cliok "start"
varnish v1 -wait-running
sema r1 sync 2

delay 0.5

client c1 {
	txreq -url "/"
	rxresp
	expect resp.http.foo == "foo"
} -run
