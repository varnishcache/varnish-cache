varnishtest "Test vcl_ban"

server s1 {
	rxreq
	txresp -status 200 -body "ban"
} -start

varnish v1 -arg "-p feature=-vcl_ban" -vcl+backend {
	sub vcl_recv {
		 ban("obj.status == 0");
	}
} -start

logexpect l1 -v v1 {
	expect * 1001 VCL_Error "ban\\(\\): Feature flag vcl_ban is off"
} -start

client c1 {
	txreq -url "/ban"
	rxresp
	expect resp.status == 503
} -run

logexpect l1 -wait

varnish v1 -cliok "param.set feature +vcl_ban"

client c2 {
	txreq -url "/ban"
	rxresp
	expect resp.status == 200
} -run

varnish v1 -cliok "param.set feature -vcl_ban"

logexpect l2 -v v1 {
	expect * * VCL_Error "ban\\(\\): Feature flag vcl_ban is off"
} -start

client c3 {
	txreq -url "/ban"
	rxresp
	expect resp.status == 503
} -run

logexpect l2 -wait
