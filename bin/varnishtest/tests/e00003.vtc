varnishtest "ESI include"


server s1 {
	rxreq
	txresp -body {
		<html>
		Before include
		<esi:include src="/body" sr="foo"/>
		After include
	}
	rxreq
	expect req.url == "/body1"
	txresp -body {
		Included file
	}
} -start

varnish v1 -vcl+backend {
	sub vcl_recv {
		if (req.esi_level > 0) {
			set req.url = req.url + req.esi_level;
		}
	}
	sub vcl_backend_response {
		set beresp.do_esi = true;
	}
} -start

client c1 {
	txreq
	rxresp
	expect resp.bodylen == 65
	expect resp.status == 200
	# test that there is no difference on miss/hit
	txreq
	rxresp
	expect resp.bodylen == 65
	expect resp.status == 200
}

client c1 -run
varnish v1 -expect esi_errors == 0
