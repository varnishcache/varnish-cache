# $Id$

test "VCL/VRT: url/request/proto/response/status"


server s1 {
	rxreq 
	txresp -hdr "Connection: close" -body "012345\n"
}

server s1 -start 

varnish v1 -vcl+backend {
	sub vcl_recv {
		set req.http.foobar =
		    req.url +
		    req.request +
		    req.proto;
		set req.url = "/";
		set req.proto = "HTTP/1.2";
		set req.request = "GET";
	}
	sub vcl_miss {
		set bereq.http.foobar =
		    bereq.url +
		    bereq.proto;
		set bereq.url = "/";
		set bereq.proto = "HTTP/1.2";
		set bereq.request = "GET";
	}
	sub vcl_fetch {
		set beresp.http.foobar =
		    beresp.proto + beresp.response + beresp.status;
		set beresp.proto = "HTTP/1.2";
		set beresp.response = "For circular files";
		set beresp.status = 903;
	}
	sub vcl_deliver {
		set resp.proto = "HTTP/1.2";
		set resp.response = "Naah, lets fail it";
		set resp.status = 904;
		# XXX should be moved to it's own test
		set resp.http.x-served-by-hostname = server.hostname;
		set resp.http.x-served-by-identity = server.identity;
		set resp.http.foobar = resp.proto + resp.status;
	}
} -start

client c1 {
	txreq -url "/"
	rxresp
	expect resp.status == 904
}

client c1 -run

server s1 -wait

varnish v1 -stop
