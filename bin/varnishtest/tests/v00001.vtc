varnishtest "VCL/VRT: url/request/proto/reason/status"


server s1 {
	rxreq
	txresp -hdr "Connection: close" -body "012345\n"
}

server s1 -start

varnish v1 -vcl+backend {
	sub vcl_recv {
		set req.http.foobar =
		    req.url +
		    req.method +
		    req.proto;
		set req.url = "/";
		set req.proto = "HTTP/1.2";
		set req.method = "GET";
	}
	sub vcl_backend_fetch {
		set bereq.http.foobar =
		    bereq.url +
		    bereq.proto;
		set bereq.url = "/";
		set bereq.proto = "HTTP/1.2";
		set bereq.method = "GET";
	}
	sub vcl_backend_response {
		set beresp.http.foobar =
		    beresp.proto + beresp.reason + beresp.status;
		set beresp.proto = "HTTP/1.2";
		set beresp.reason = "For circular files";
		set beresp.status = 903;
	}
	sub vcl_deliver {
		set resp.proto = "HTTP/1.2";
		set resp.reason = "Naah, lets fail it";
		set resp.status = 904;
		# XXX should be moved to it's own test
		set resp.http.x-served-by-hostname = server.hostname;
		set resp.http.x-served-by-identity = server.identity;
		set resp.http.foobar = resp.proto + resp.status;
	}
} -start

client c1 {
	txreq -url "/"
	rxresp
	expect resp.status == 904
}

client c1 -run

server s1 -wait

varnish v1 -stop
